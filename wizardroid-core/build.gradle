apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 18
    buildToolsVersion "18.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 18
    }
}

dependencies {
    compile 'com.android.support:support-v4:18.0.0'
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
	configuration = configurations.archives

    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            //Sonatype related variables are defined in ~/.gradle/gradle.properties
            repository(url: isReleaseVersion ? sonatypeRepoStaging : sonatypeRepoSnapshots) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
			}
			pom.project {
				name 'WizarDroid'
				packaging 'aar'
				description 'Lightweight Android library for creating step by step wizards'
				url 'http://wizardroid.codepond.org'

				scm {
					url 'http://www.github.com/nimrodda/wizardroid'
					connection 'scm:git://github.com/Nimrodda/WizarDroid.git'
					developerConnection 'scm:git://github.com/Nimrodda/WizarDroid.git'
				}

				licenses {
					license {
						name 'The MIT License (MIT)'
						url 'https://raw.github.com/Nimrodda/WizarDroid/master/license'
						distribution 'repo'
					}
				}

                developers {
                    developer {
                        id 'nimrodda'
                        name 'Nimrod Dayan'
                        email 'feedback@codepond.org'
                    }
                }
			}
        }
    }
}
